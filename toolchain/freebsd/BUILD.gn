# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/gcc_toolchain.gni")

template("freebsd_toolchain") {
  gcc_toolchain(target_name) {
    cc = "clang"
    cxx = "clang++"

    readelf = "readelf"
    nm = "nm"
    ar = "llvm-ar"
    ld = cxx

    toolchain_args = {
      current_os = "freebsd"

      is_clang = true

      if (defined(invoker.toolchain_args)) {
        forward_variables_from(invoker.toolchain_args, "*")
      }
    }
  }
}

freebsd_toolchain("x64") {
  toolchain_args = {
    current_cpu = "x64"
  }
}

freebsd_toolchain("arm64") {
  toolchain_args = {
    current_cpu = "arm64"
  }
}
